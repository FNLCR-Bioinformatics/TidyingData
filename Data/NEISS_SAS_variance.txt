/*****************************************************************/
/* SAS program to input NEISS data downloaded from the Internet. */
/* Change the FILENAME to your directory and data set name.      */
/*                                                               */
/* This program also gives examples of how to compute            */
/* a national estimate and variance using the NEISS data.        */
/*****************************************************************/;

FILENAME NEISS "C:\NEISS\nss2002.tsv";

DATA NEISS;
   INFILE NEISS
   delimiter='09'x MISSOVER DSD lrecl=32767 firstobs=2 ;
   format nek best12. ;		format trmt_date $10. ;			format psu 3. ;
   format weight 8.4 ;		format stratum $1. ;			format age 3. ;
   format sex 1. ;		format race 1. ;			format race_other $15. ;
   format diag 2. ;		format diag_other $20. ;		format body_part 2. ;
   format disposition 1. ;	format location 1. ;			format fmv 1. ;
   format prod1 4. ;		format prod2 4. ;			format narr1 $71. ;
   format narr2 $71. ;

   input
   nek			trmt_date $		psu			weight
   stratum $		age			sex			race
   race_other $		diag			diag_other $		body_part
   disposition		location		fmv			prod1
   prod2		narr1 $ 		narr2 $;

DATA NEISS; SET NEISS;
   NEISS = 1;
   LABEL NEISS = 'Variable used to calculate overall variance';

   LABEL NEK = 'CPSC Case Number';
   LABEL TRMT_DATE = 'Treatment Date';
   LABEL PSU = 'Hospital';
   LABEL WEIGHT = 'Statistical Weight';
   LABEL STRATUM = 'Hospital Stratum';
   LABEL AGE = 'Age';
   LABEL SEX = 'Gender';
   LABEL RACE = 'Race'; 
   LABEL RACE_OTHER = 'Race/Ethnicity'; 
   LABEL DIAG = 'Injury Diagnosis'; 
   LABEL DIAG_OTHER = 'Diagnosis Other'; 
   LABEL BODY_PART = 'Body Part';
   LABEL DISPOSITION = 'Injury Disposition'; 
   LABEL LOCATION = 'Event Locale';
   LABEL FMV = 'Fire Department Attended';  
   LABEL PROD1 = 'Product';
   LABEL PROD2 = 'Product 2';
   LABEL NARR1 = 'NEISS narrative';
   LABEL NARR2 = 'NEISS narrative 2';

RUN;

/******************************************************************/
/* Create a dummy record for every hospital to assure the correct */
/* degrees of freedom are used when calculating a variance        */
/******************************************************************/;

DATA DUMMY;
   INPUT PSU STRATUM $;
   CARDS;
1	S
2	V
3	L
4	S
5	L
6	S
7	S
8	C
9	S
10	C
11	S
12	S
13	S
14	M
15	S
16	M
17	V
18	C
19	S
20	C
21	V
22	V
23	V
24	M
25	V
26	V
27	S
28	S
29	S
30	M
31	C
32	C
33	V
34	V
35	V
36	S
37	C
38	V
39	S
40	V
41	V
42	L
43	S
44	L
45	S
46	S
47	S
48	S
49	S
50	L
51	L
52	M
53	V
54	M
55	L
56	S
57	V
58	V
59	S
60	S
61	V
62	S
63	M
64	V
65	S
66	S
67	V
68	M
69	L
70	S
71	S
72	S
73	S
74	S
75	S
76	M
77	V
78	M
79	S
80	S
81	S
82	S
84	S
85	S
86	S
87	S
88	M
89	L
90	C
91	V
92	S
93	V
94	S
95	V
96	S
97	S
98	S
99	S
100	M
101	M
;

/* SET DUMMY WT T0 POSITIVE VALUE */;

DATA DUMMY; SET DUMMY;
   NEISS = 1;
   WEIGHT = 0.0001;

DATA NEISS; SET NEISS DUMMY;

/*****************************************************/
/* Sample PROC SURVEYMEANS Variance Calculations     */
/*   1. Calculate Overall NEISS National Estimate    */
/*   2. Calculate NEISS National Estimates By Gender */
/*****************************************************/;

/******************************************************/
/* On the output of PROC SURVEYMEANS:                 */
/*   1. The number of strata should equal 5           */
/*   2. The number of clusters should equal 100       */
/*   3. The number of observations will be 100 more   */
/*      than the actual number of NEISS cases because */ 
/*      of the 100 dummy records that were added.     */
/******************************************************/;

PROC SURVEYMEANS DATA = NEISS SUM VARSUM;
   CLUSTER PSU;
   STRATA STRATUM;
   WEIGHT WEIGHT;
   VAR NEISS;
   TITLE1 'VARIANCE ESTIMATES FOR NEISS';
   TITLE2 'SAS: PROC SURVEYMEANS';

PROC SURVEYMEANS DATA = NEISS SUM VARSUM;
   CLASS SEX;
   CLUSTER PSU;
   STRATA STRATUM;
   WEIGHT WEIGHT;
   VAR SEX;
   TITLE1 'VARIANCE ESTIMATES FOR NEISS';
   TITLE2 'SAS: PROC SURVEYMEANS';
   TITLE3 'BY GENDER';

RUN;

